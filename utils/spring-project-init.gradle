gradle.projectsLoaded {
    rootProject {
        allprojects {
            afterEvaluate {
                tasks.artifactoryPublish.enabled(false)
                pluginManager.withPlugin('maven-publish') {
                    tasks.artifactoryPublish {
                        enabled(true)
                        properties {
                            mavenJava '*:*:*:*@zip', 'zip.name': project.name, 'zip.displayname': project.description, 'zip.deployed': 'false'
                            mavenJava '*:*:*:docs@zip', 'zip.type': 'docs'
                            mavenJava '*:*:*:dist@zip', 'zip.type': 'dist'
                        }
                        publications(publishing.publications.mavenJava)
                        clientConfig.connectionRetries = 4
                    }
                }
            }
        }

        // No need to have this task in project: only when we perform GHA
        tasks.register('nextDevelopmentVersion') {
            doLast {
                if (!project.version.endsWith('-SNAPSHOT')) {
                    String nextDevelopmentVersion
                    if (project.version.contains('-M') || project.version.contains('-RC')) {
                        nextDevelopmentVersion = project.version - ~/(M|RC)\d+/ + 'SNAPSHOT'
                    } else {
                        def versionParts = project.version.tokenize('.')
                        def major = versionParts[0]
                        def minor = versionParts[1]
                        def patch = versionParts[2] as int
                        nextDevelopmentVersion = major + '.' + minor + '.' + (++patch) + '-SNAPSHOT'
                    }

                    def gradleProperties = file('gradle.properties')
                    def newProps = gradleProperties.text.replace(project.version, nextDevelopmentVersion)
                    gradleProperties.text = newProps
                }
            }
        }
    }
}
